/*
 * Copyright 2013 Chris Banes
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'maven-publish'
apply plugin: 'signing'

def isReleaseBuild() {
    return !"{{artifactVersion}}".contains("SNAPSHOT")
}

def getReleaseRepositoryUrl() {
    return hasProperty('RELEASE_REPOSITORY_URL') ? RELEASE_REPOSITORY_URL
            : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

def getSnapshotRepositoryUrl() {
    return hasProperty('SNAPSHOT_REPOSITORY_URL') ? SNAPSHOT_REPOSITORY_URL
            : "https://oss.sonatype.org/content/repositories/snapshots/"
}

def getRepositoryUsername() {
    return hasProperty('OSSRH_USERNAME') ? OSSRH_USERNAME : System.getenv("OSSRH_USERNAME")
}

def getRepositoryPassword() {
    return hasProperty('OSSRH_PASSWORD') ? OSSRH_PASSWORD : System.getenv("OSSRH_PASSWORD")
}

afterEvaluate { project ->
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = "{{groupId}}"
                artifactId = "{{artifactId}}"
                version = "{{artifactVersion}}"

                pom {
                    name = "{{groupId}}:{{artifactId}}"
                    packaging = "aar"
                    description = "{{artifactDescription}}"
                    url = "{{artifactUrl}}"

                    scm {
                        url = "{{scmUrl}}"
                        connection = "{{scmConnection}}"
                        developerConnection = "{{scmDeveloperConnection}}"
                    }

                    licenses {
                        license {
                            name = "{{licenseName}}"
                            url = "{{licenseUrl}}"
                            distribution = "repo"
                        }
                    }

                    developers {
                        developer {
                            name = "{{developerName}}"
                            email = "{{developerEmail}}"
                            organization = "{{developerOrganization}}"
                            organizationUrl = "{{developerOrganizationUrl}}"
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                url = isReleaseBuild() ? getReleaseRepositoryUrl() : getSnapshotRepositoryUrl()

                credentials {
                    username = getRepositoryUsername()
                    password = getRepositoryPassword()
                }
            }
        }
    }

    signing {
        required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
        sign publishing.publications.release
    }
}